# METADATA
# description: Policy for listing identity groups
package identity.group.list

import data.identity

# List identity groups.
#
# The `input.target.group` contains query parameters:
#   domain_id: string (optional)  Filter users by Domain ID.
#   name:      string (optional)  Filter users by Name.
#
# The `input.existing` is null
#
default allow := false

allow if {
	"admin" in input.credentials.roles
}

allow if {
	input.credentials.is_admin
}

allow if {
	"reader" in input.credentials.roles
	input.credentials.system == "all"
}

allow if {
	"reader" in input.credentials.roles
	identity.domain_matches_domain_scope
}

violation contains {"field": "domain_id", "msg": "listing user groups in domain different to the domain scope requires `admin` role."} if {
	not "admin" in input.credentials.roles
	"manager" in input.credentials.roles
	not identity.domain_matches_domain_scope
}

violation contains {"field": "domain_id", "msg": "listing user groups requires a reader role with the domain scope."} if {
	not "admin" in input.credentials.roles
	not "reader" in input.credentials.roles
	identity.domain_matches_domain_scope
}
