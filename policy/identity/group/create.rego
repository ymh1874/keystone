# METADATA
# description: Policy for creating identity groups
package identity.group.create

import data.identity

# Create a new user group
#
# The `input.target.group` is the new group object (GroupCreate):
#   domain_id:    string        Group domain ID.
#   name:         string        Group name.
#   description:  string (optional) Group description.
#
# The `input.existing` is null
#
default allow := false

allow if {
	"admin" in input.credentials.roles
}

allow if {
	"manager" in input.credentials.roles
	identity.domain_matches_domain_scope
}

violation contains {"field": "domain_id", "msg": "creating a new user group in domain different to the domain scope requires `admin` role."} if {
	not "admin" in input.credentials.roles
	"manager" in input.credentials.roles
	not identity.domain_matches_domain_scope
}

violation contains {"field": "domain_id", "msg": "creating a new user group requires a manager role with the domain scope."} if {
	not "admin" in input.credentials.roles
	not "manager" in input.credentials.roles
	identity.domain_matches_domain_scope
}
