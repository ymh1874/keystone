# METADATA
# description: Policy for deleting users
package identity.user.delete

import data.identity

# Delete user.
#
# The `input.target.user` is the stored user object:
#   default_project_id:  string (optional)  The ID of the default project for the user.
#   domain_id:           string            User domain ID.
#   enabled:             bool              If the user is enabled.
#   id:                  string            User ID.
#   name:               string            User name.
#   options:             object (optional)  The resource options for the user.
#   password_expires_at: string (optional)  The date and time when the password expires.
#
# The `input.existing` is null
#
default allow := false

allow if {
	"admin" in input.credentials.roles
}

allow if {
	"manager" in input.credentials.roles
	identity.domain_matches_domain_scope
}

violation contains {"field": "domain_id", "msg": "removing a user in domain different to the domain scope requires `admin` role."} if {
	not "admin" in input.credentials.roles
	"manager" in input.credentials.roles
	not identity.domain_matches_domain_scope
}

violation contains {"field": "domain_id", "msg": "removing a user requires a manager role with the domain scope."} if {
	not "admin" in input.credentials.roles
	not "manager" in input.credentials.roles
	identity.domain_matches_domain_scope
}
